/-
Copyright 2025 The Formal Conjectures Authors.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    https://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-/
import Mathlib.Data.Set.Card

/-!
# VC dimension of a set family

This file defines the VC dimension of a set family as the maximum size of a set it shatters.
-/

variable {Î± : Type*} {ğ’œ â„¬ : Set (Set Î±)} {A B : Set Î±} {a : Î±} {d d' : â„•}

variable (ğ’œ A) in
/-- A set family `ğ’œ` shatters a set `A` if restricting `ğ’œ` to `A` gives rise to all subsets of `A`.
-/
def Shatters : Prop := âˆ€ â¦ƒBâ¦„, B âŠ† A â†’ âˆƒ C âˆˆ ğ’œ, A âˆ© C = B

lemma Shatters.exists_inter_eq_singleton (hs : Shatters ğ’œ A) (ha : a âˆˆ A) : âˆƒ B âˆˆ ğ’œ, A âˆ© B = {a} :=
  hs <| Set.singleton_subset_iff.2 ha

lemma Shatters.mono_left (h : ğ’œ âŠ† â„¬) (hğ’œ : Shatters ğ’œ A) : Shatters â„¬ A :=
  fun _B hB â†¦ let âŸ¨u, hu, hutâŸ© := hğ’œ hB; âŸ¨u, h hu, hutâŸ©

lemma Shatters.mono_right (h : B âŠ† A) (hs : Shatters ğ’œ A) : Shatters ğ’œ B := fun u hu â†¦ by
  obtain âŸ¨v, hv, rflâŸ© := hs (hu.trans h); exact âŸ¨v, hv, inf_congr_right hu <| inf_le_of_left_le hâŸ©

lemma Shatters.exists_superset (h : Shatters ğ’œ A) : âˆƒ B âˆˆ ğ’œ, A âŠ† B :=
  let âŸ¨B, hB, hstâŸ© := h .rfl; âŸ¨B, hB, Set.inter_eq_left.1 hstâŸ©

lemma Shatters.of_forall_subset (h : âˆ€ B, B âŠ† A â†’ B âˆˆ ğ’œ) : Shatters ğ’œ A :=
  fun B hB â†¦ âŸ¨B, h _ hB, Set.inter_eq_right.2 hBâŸ©

protected lemma Shatters.nonempty (h : Shatters ğ’œ A) : ğ’œ.Nonempty :=
  let âŸ¨B, hB, _âŸ© := h .rfl; âŸ¨B, hBâŸ©

@[simp] lemma shatters_empty : Shatters ğ’œ âˆ… â†” ğ’œ.Nonempty :=
  âŸ¨Shatters.nonempty, fun âŸ¨A, hsâŸ© B hB â†¦ âŸ¨A, hs, by simpa [eq_comm] using hBâŸ©âŸ©

protected lemma Shatters.subset_iff (h : Shatters ğ’œ A) : B âŠ† A â†” âˆƒ u âˆˆ ğ’œ, A âˆ© u = B :=
  âŸ¨fun hB â†¦ h hB, by rintro âŸ¨u, _, rflâŸ©; exact Set.inter_subset_leftâŸ©

lemma shatters_iff : Shatters ğ’œ A â†” (A âˆ© Â·) '' ğ’œ = A.powerset :=
  âŸ¨fun h â†¦ by ext B; simp [h.subset_iff], fun h B hB â†¦ h.superset hBâŸ©

lemma univ_shatters : Shatters .univ A := .of_forall_subset <| by simp

@[simp] lemma shatters_univ : Shatters ğ’œ .univ â†” ğ’œ = .univ := by
  simp [Shatters, Set.eq_univ_iff_forall]

variable (ğ’œ d) in
/-- A set family `ğ’œ` has VC dimension at most `d` if there are no families `x` of elements indexed
by `[d + 1]` and `A` of sets of `ğ’œ` indexed by `2^[d + 1]` such that `x i âˆˆ A s â†” i âˆˆ s` for all
`i âˆˆ [d], s âŠ† [d]`. -/
def HasVCDimAtMost : Prop :=
  âˆ€ (x : Fin (d + 1) â†’ Î±) (A : Set (Fin (d + 1)) â†’ Set Î±),
    (âˆ€ s, A s âˆˆ ğ’œ) â†’ Â¬ âˆ€ i s, x i âˆˆ A s â†” i âˆˆ s

lemma HasVCDimAtMost.anti (hğ’œâ„¬ : ğ’œ â‰¤ â„¬) (hâ„¬ : HasVCDimAtMost â„¬ d) : HasVCDimAtMost ğ’œ d :=
  fun _x _A hA â†¦ hâ„¬ _ _ fun _s â†¦ hğ’œâ„¬ <| hA _

lemma HasVCDimAtMost.mono (h : d â‰¤ d') (hd : HasVCDimAtMost ğ’œ d) : HasVCDimAtMost ğ’œ d' := by
  rintro x A hA hxA
  replace h : d + 1 â‰¤ d' + 1 := by omega
  exact hd (x âˆ˜ Fin.castLE h) (A âˆ˜ Set.image (Fin.castLE h)) (by simp [hA]) fun i s â†¦
    (hxA ..).trans <| by simp

@[simp] lemma HasVCDimAtMost.empty : HasVCDimAtMost (âˆ… : Set (Set Î±)) d := by simp [HasVCDimAtMost]

proof_wanted hasVCDimAtMost_iff_shatters : HasVCDimAtMost ğ’œ d â†” âˆ€ A, Shatters ğ’œ A â†’ A.encard â‰¤ d
